plugins {
	id "java"
	id "org.springframework.boot" version "3.3.4"
	id "io.spring.dependency-management" version "1.1.6"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-web"
	testImplementation "org.springframework.boot:spring-boot-starter-test"
	implementation "org.springframework.boot:spring-boot-starter-data-jpa"
	implementation "org.xerial:sqlite-jdbc:3.46.1.3"
	implementation "org.hibernate.orm:hibernate-community-dialects"
	implementation 'org.mapstruct:mapstruct:1.6.3'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
}

tasks.register("prepareUiResources", Copy) {
	description = "Copy UI build output into server resources for packaging"
	dependsOn(":ui:buildUi")
	from(project(":ui").layout.projectDirectory.dir("dist"))
	into(layout.buildDirectory.dir("resources/main/static"))
	mustRunAfter(tasks.named("processResources"))
}

tasks.named("bootJar") {
	dependsOn(tasks.named("prepareUiResources"))
}

